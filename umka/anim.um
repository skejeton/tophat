// Module for frame based image animation.
// TODO: Rework this module to use atlases.

import (
	"th.um"
	"image.um"
	"rect.um"
)

// Anim is able to animate an array of images. As opposed to ent.Ent.animate,
// it starts from the first frame.
type Anim* = struct {
	anim: []image.Image
	rot: th.fu
	scale: th.fu
	fps: th.uu
	looping: bool

	pos: th.Vf2

	zeroTime: int

	frame: int
}

// Anim's constructor.
fn mk*(pos: th.Vf2, anm: []image.Image, fps: th.uu): Anim {
	a := Anim{}

	a.anim = append(anm, anm[len(anm)-1])

	a.fps = fps
	a.scale = 1
	a.rot = 0

	a.pos = pos

	a.looping = false

	return a
}

// Starts animation with start_time of t.
fn (a: ^Anim) start*(t: int) {
	a.zeroTime = t
}

// This draws the animation. Returns true, if the animation ended.
fn (a: ^Anim) handle*(t: uint, cam: rect.Rect): bool {
	a.frame = (round(real(t-a.zeroTime)/round((1000.0/a.fps))) % len(a.anim))

	a.anim[a.frame].draw(
		th.Transform{
			th.Vf2{round(a.pos.x) - cam.x + cam.w/2, round(a.pos.y) - cam.y + cam.h/2},
			th.Vf2{a.scale, a.scale}, th.Vf2{}, a.rot})

	if a.frame == len(a.anim) - 1 {
		if a.looping {
			a.zeroTime = t
		} else {
			return true
		}
	}

	return false
}
